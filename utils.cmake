function(check_path p out)
    if(EXISTS "${p}")
        set(${out} "${p}" PARENT_SCOPE)
    else()
        set(${out} "" PARENT_SCOPE)
    endif()
endfunction()

macro(set_output_dir dir)
    foreach(t ${CMAKE_CONFIGURATION_TYPES})
	    string(TOUPPER ${t} tu)
	    string(TOLOWER ${t} tl)
	    set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_${tu} "${dir}/${tl}")
	    set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_${tu} "${dir}/${tl}")
	    set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_${tu} "${dir}/${tl}")
    endforeach()
endmacro()

function(assign_source_group parent_path folder_prefix)
    foreach(_source IN ITEMS ${ARGN})
        if (IS_ABSOLUTE "${_source}")
            file(RELATIVE_PATH _source_rel "${parent_path}" "${_source}")
        else()
            set(_source_rel "${_source}")
        endif()
        get_filename_component(_source_path "${_source_rel}" PATH)
        string(REPLACE "/" "\\" _source_path_msvc "${_source_path}")
        source_group("${folder_prefix}${_source_path_msvc}" FILES "${_source}")
    endforeach()
endfunction()

function(generate_typeinfo tar desc)
    if(tar STREQUAL "flame_foundation")
        file(GENERATE OUTPUT "$<TARGET_FILE_DIR:${tar}>/${tar}.typedesc" CONTENT "${CMAKE_SOURCE_DIR}/source/foundation\n${desc}" TARGET ${tar})
        add_custom_command(TARGET typeinfogen_dep POST_BUILD COMMAND $<TARGET_FILE:typeinfogen> $<TARGET_FILE:flame_foundation>)
    else()
        file(GENERATE OUTPUT "$<TARGET_FILE_DIR:${tar}>/${tar}.typedesc" CONTENT "${CMAKE_CURRENT_SOURCE_DIR}\n${desc}" TARGET ${tar})
        add_dependencies(${tar} typeinfogen_dep)
        add_custom_command(TARGET ${tar} POST_BUILD COMMAND $<TARGET_FILE:typeinfogen> $<TARGET_FILE:${tar}>)
    endif()
endfunction()

function(generate_config)
	file(WRITE ${CMAKE_CURRENT_BINARY_DIR}/config.ini 
		"developing = 1\n"
		"resource_path = \"${CMAKE_CURRENT_SOURCE_DIR}\"\n"
		"engine_path = {e}\n"
	)
endfunction()
