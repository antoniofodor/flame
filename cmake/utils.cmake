function(group_source src_list file_prefix group_prefix) # group src files in vs filters as how they look like in filesystem
	if (WIN32)
		foreach(FILE ${src_list}) 
			get_filename_component(PARENT_DIR "${FILE}" DIRECTORY)
			string(REPLACE "${CMAKE_CURRENT_SOURCE_DIR}${file_prefix}" "" GROUP "${PARENT_DIR}")
			string(REPLACE "${CMAKE_SOURCE_DIR}" "" GROUP "${GROUP}")
			string(REPLACE "/" "\\" GROUP "${GROUP}")
			source_group("${group_prefix}${GROUP}" FILES "${FILE}")
		endforeach()
	endif()
endfunction()

macro(set_output_dir dir)
	foreach(c ${CMAKE_CONFIGURATION_TYPES})
		string(TOUPPER ${c} c )
		set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_${c} ${dir})
		set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_${c} ${dir})
		set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_${c} ${dir})
	endforeach()
endmacro()

function(add_static_lib name source_list file_prefix folder)
	group_source("${source_list}" "${file_prefix}" "Source")
	add_library(${name} STATIC ${source_list})
	set_target_properties(${name} PROPERTIES FOLDER ${folder})
endfunction()

function(add_lib name source_list file_prefix folder)
	group_source("${source_list}" "${file_prefix}" "Source")
	add_library(${name} SHARED ${source_list})
	set_target_properties(${name} PROPERTIES FOLDER ${folder})
endfunction()

function(add_exe name source_list file_prefix folder)
	group_source("${source_list}" "${file_prefix}" "Source")
	add_executable(${name} ${source_list})
	set_target_properties(${name} PROPERTIES FOLDER ${folder})
	set_target_properties(${name} PROPERTIES VS_DEBUGGER_WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}/bin")
endfunction()

