layout
  @pll
shaders
  ../fullscreen.vert
 ---
  @frag
renderpass
  {rp}
cull_mode
  None
  
@pll
layout (push_constant) uniform PushConstant
{
	vec2 screen_size;
	uint channel;
	float distance;
	float merge_k;
}pc;

struct SdCircle
{
	vec2 coord;
	float radius;
};

struct SdOriRect
{
	vec2 point_a;
	vec2 point_b;
	float thickness;
};

layout(set = SET, binding = 0) buffer readonly SDF
{
	uint circles_count;
	uint ori_rects_count;
	SdCircle circles[256];
	SdOriRect ori_rects[256];
}sdf;

layout (set = SET, binding = 1) uniform sampler2D img_src;
@

@frag
#include "../math.glsl"

layout (location = 0) in vec2 i_uv;

layout (location = 0) out vec4 o_color;

float scene_sdf(vec2 p, float merge_k)
{
	float ret = 100000000.0;
	for (int i = 0; i < sdf.circles_count; i++)
	{
		SdCircle d = sdf.circles[i];
		ret = op_smooth_union(ret, sd_circle(p - d.coord, d.radius), pc.merge_k);
	}
	for (int i = 0; i < sdf.ori_rects_count; i++)
	{
		SdOriRect d = sdf.ori_rects[i];
		ret = op_smooth_union(ret, sd_ori_box(p, d.point_a, d.point_b, d.thickness), pc.merge_k);
	}
	return ret;
}

void main()
{
	vec2 pos = i_uv * pc.screen_size;
	float d = scene_sdf(pos, pc.merge_k);
	float value = d < pc.distance ? (pc.distance - max(d, 0.0)) / pc.distance : 0.0;
	vec4 color = texture(img_src, i_uv);
	color *= 1.0 - value;
	color[pc.channel] = value;
	o_color = color;
}
@
